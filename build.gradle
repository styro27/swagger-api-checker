plugins {
    id 'java'
    id 'io.qameta.allure' version '2.8.0'
    id 'org.openapi.generator' version '7.5.0'
}

group = 'org.example'
version = '1.0-SNAPSHOT'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
}

repositories {
    mavenCentral()
}

configurations {
    compileOnly.extendsFrom annotationProcessor
    testCompileOnly.extendsFrom annotationProcessor
    testAnnotationProcessor.extendsFrom annotationProcessor
    testCompile.extendsFrom testCompileOnly
}

dependencies {
    // JUnit ()
    implementation platform('org.junit:junit-bom:5.9.2')
    implementation 'org.junit.jupiter:junit-jupiter-api'
    implementation 'org.junit.jupiter:junit-jupiter-params'
    implementation 'org.junit.jupiter:junit-jupiter-engine'

    // Rest Assured + Swagger validation
    implementation 'io.rest-assured:rest-assured:5.3.2'
    implementation 'com.atlassian.oai:swagger-request-validator-restassured:2.4.2'
    implementation 'io.qameta.allure:allure-rest-assured:2.13.9'

    // AssertJ
    testImplementation 'org.assertj:assertj-core:3.24.2'

    // Allure JUnit 5
    testImplementation 'io.qameta.allure:allure-junit5:2.13.8'
    implementation 'io.qameta.allure:allure-java-commons:2.13.8'

    // Owner
    implementation 'org.aeonbits.owner:owner-java8:1.0.12'

    // Jackson
    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.1'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.17.1'

    //Datafaker
    implementation 'net.datafaker:datafaker:2.4.2'


    implementation 'org.yaml:snakeyaml:1.33'
    testImplementation 'org.yaml:snakeyaml:1.33'
    configurations.all {
        resolutionStrategy {
            force 'org.yaml:snakeyaml:1.33'
        }
    }


    implementation 'javax.annotation:javax.annotation-api:1.3.2'

}
task generateModels(type: org.openapitools.generator.gradle.plugin.tasks.GenerateTask) {
    generatorName = "java"
    inputSpec = "$projectDir/src/main/resources/swagger.json"
    outputDir = "$buildDir/generated"
    modelPackage = "com.example.petstore.model"

    globalProperties = [
            models         : "",
            apis           : "false",
            supportingFiles: "false"
    ]

    configOptions = [
            dateLibrary: "java17",
            library    : "native"
    ]
}

task cleanGeneratedApi(type: Delete) {
    delete fileTree("$buildDir/generated/src/main/java") {
        include '**/api/**'
        include '**/ApiClient.java'
    }
}
generateModels.finalizedBy cleanGeneratedApi
sourceSets {
    main.java.srcDirs += "$buildDir/generated/src/main/java"
}

test {
    useJUnitPlatform()
    if (rootProject.hasProperty('profile')) {
        environment("profile", profile)
    } else {
        println("The property 'profile' was not found! The default value will be 'test'")
        environment "profile", "test"
    }
}

allure {
    version = '2.8.0'
    autoconfigure = true
    useJUnit5 {
        version = '2.8.0'
    }
}
